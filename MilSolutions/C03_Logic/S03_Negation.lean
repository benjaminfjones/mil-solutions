import MilSolutions.Common
import Mathlib.Data.Real.Basic

namespace C03S03

section
variable (a b : ℝ)

example (h : a < b) : ¬b < a := by
  intro h'
  have : a < a := lt_trans h h'
  apply lt_irrefl a this

def FnUb (f : ℝ → ℝ) (a : ℝ) : Prop :=
  ∀ x, f x ≤ a

def FnLb (f : ℝ → ℝ) (a : ℝ) : Prop :=
  ∀ x, a ≤ f x

def FnHasUb (f : ℝ → ℝ) :=
  ∃ a, FnUb f a

def FnHasLb (f : ℝ → ℝ) :=
  ∃ a, FnLb f a

variable (f : ℝ → ℝ)

example (h : ∀ a, ∃ x, f x > a) : ¬FnHasUb f := by
  intro fnub
  rcases fnub with ⟨a, fnuba⟩
  rcases h a with ⟨x, hx⟩
  have : f x ≤ a := fnuba x
  linarith

example (h : ∀ a, ∃ x, f x < a) : ¬FnHasLb f := by
  rintro ⟨ a, flba ⟩
  rcases h a with ⟨ w, hw ⟩
  have haw : a ≤ f w := flba w
  -- linarith closes the goal here
  have : a < a := lt_of_le_of_lt haw hw
  apply lt_irrefl a this

example : ¬FnHasUb fun x ↦ x := by
  rintro ⟨ a, flba ⟩
  have : a + 1 ≤ a := flba (a+1)
  linarith

#check (not_le_of_gt : a > b → ¬a ≤ b)
#check (not_lt_of_ge : a ≥ b → ¬a < b)
#check (lt_of_not_ge : ¬a ≥ b → a < b)
#check (le_of_not_gt : ¬a > b → a ≤ b)

example (h : Monotone f) (h' : f a < f b) : a < b := by
  apply lt_of_not_ge
  intro hab
  have : f b < f b :=
    calc
      f b ≤ f a := by apply h hab
        _ < f b := by assumption
  exact lt_irrefl (f b) this

example (h : a ≤ b) (h' : f b < f a) : ¬Monotone f := by
  intro hm
  have : f b < f b :=
    calc
      f b < f a := by assumption
        _ ≤ f b := by apply hm h
  exact lt_irrefl (f b) this

example : ¬∀ {f : ℝ → ℝ}, Monotone f → ∀ {a b}, f a ≤ f b → a ≤ b := by
  intro h
  let g := fun _ : ℝ ↦ (0 : ℝ)
  have monof : Monotone g := by
    intro a b _
    apply le_refl  -- `g a` and `g b` are unfolded automatically
  have h' : g 1 ≤ g 0 := le_refl _
  have h'' : (1 : ℝ) ≤ 0 := h monof h'
  have obv : (0 : ℝ) < 1 := by norm_num
  exact not_le_of_gt obv h''

example (x : ℝ) (h : ∀ ε > 0, x < ε) : x ≤ 0 := by
  apply le_of_not_gt
  intro hxp
  let y := x/2
  have hylx : y < x := div_two_lt_of_pos hxp
  have hyp : 0 < y := half_pos hxp
  have hxly : x < y := h y hyp
  apply lt_irrefl y
  show y < y
  calc
    y < x := hylx
    _ < y := hxly

end

section
variable {α : Type*} (P : α → Prop) (Q : Prop)

example (h : ¬∃ x, P x) : ∀ x, ¬P x := by
  intro x hx
  exact h ⟨ x, hx ⟩

example (h : ∀ x, ¬P x) : ¬∃ x, P x := by
  rintro ⟨ w, hw ⟩
  exact h w hw

-- Uses classical (via `by_contra`), below
-- example (h : ¬∀ x, P x) : ∃ x, ¬P x := by
--   sorry

example (h : ∃ x, ¬P x) : ¬∀ x, P x := by
  intro ha
  let ⟨ w, hw ⟩ := h
  exact hw (ha w)

example (h : ¬∀ x, P x) : ∃ x, ¬P x := by
  by_contra h'
  apply h

  show ∀ x, P x
  intro x
  by_contra h''
  exact h' ⟨x, h''⟩

example (h : ¬¬Q) : Q := by
  by_contra h'
  exact h h'

example (h : Q) : ¬¬Q := by
  intro h'
  exact h' h

end

section
variable (f : ℝ → ℝ)

example (h : ¬FnHasUb f) : ∀ a, ∃ x, f x > a := by
  intro a
  by_contra hc
  -- push negation through `hc`
  -- ∀ x, f x ≤ a
  have hf : FnUb f a := by
    intro x
    by_contra h'
    apply hc
    use x
    exact lt_of_not_ge h'
  have : FnHasUb f := by use a
  exact h this

example (h : ¬∀ a, ∃ x, f x > a) : FnHasUb f := by
  push_neg at h
  exact h

example (h : ¬FnHasUb f) : ∀ a, ∃ x, f x > a := by
  dsimp only [FnHasUb, FnUb] at h
  push_neg at h
  exact h

example (h : ¬Monotone f) : ∃ x y, x ≤ y ∧ f y < f x := by
  by_contra hc
  push_neg at hc
  -- `hc` is now exactly `Monotone f`
  exact h hc

example (h : ¬FnHasUb f) : ∀ a, ∃ x, f x > a := by
  contrapose! h
  exact h

example (x : ℝ) (h : ∀ ε > 0, x ≤ ε) : x ≤ 0 := by
  contrapose! h
  use x / 2
  constructor <;> linarith

end

section
variable (a : ℕ)

example (h : 0 < 0) : a > 37 := by
  exfalso
  apply lt_irrefl 0 h

example (h : 0 < 0) : a > 37 :=
  absurd h (lt_irrefl 0)

example (h : 0 < 0) : a > 37 := by
  have : ¬0 < 0 := lt_irrefl 0
  contradiction

end
